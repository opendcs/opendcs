plugins {
    id "opendcs-rest-api.java-conventions"
    id "opendcs-rest-api.deps-conventions"
    id "opendcs-rest-api.publishing-conventions"
    id "war"
}

configurations {
    webjars
}

dependencies {
    api(libs.opendcs) {
        //Workaround for the excessive opendcs dependencies
        exclude group: '*', module: '*'
    }
    implementation(libs.servlet.api)
    implementation(libs.json.jackson)
    implementation(libs.jersey.container.servlet)
    implementation(libs.jersey.hk2)
    implementation(libs.jaxb.runtime)
    runtimeOnly(libs.postgresql)
    webjars(libs.swagger.ui)
}

task extractWebJars(type: Copy) {
    from zipTree(configurations.webjars.singleFile)

    into file("${project.layout.getBuildDirectory().get().getAsFile().toString()}/resources/main/swaggerui")
    includeEmptyDirs false
    eachFile {
        path -= ~/^.+?\/.+?\/.+?\/.+?\/.+?\//
    }
    exclude "**/swagger-initializer.js"
    includeEmptyDirs = false
}

compileTestJava {
    dependsOn extractWebJars
}

javadoc {
    dependsOn extractWebJars
}

jar {
    dependsOn extractWebJars
}

war {
    dependsOn extractWebJars
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = war.archiveBaseName.get()
            from components.java
        }
    }
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    sonarqube {
        properties {
            property 'sonar.exclusions', 'src/main/resources/swaggerui/**'
        }
    }
}
