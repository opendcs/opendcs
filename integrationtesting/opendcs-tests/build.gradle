plugins {
    id 'opendcs.java-conventions'
    id 'opendcs.deps-conventions'
    id 'opendcs.sonar-conventions'
}

def commonJacocoExcludes = [
  "**/easy_install/**",
  "**/python_packages",
  "**/certifi/**",
  "**/chardet/**",
  "**/urllib3/**",
  "**/requests/**",
  "**/idna/**",
  "**/pkg_resources/**"
]

configurations {
    extra
}

dependencies {
    implementation project(":install")
    implementation(libs.bundles.junit)
    implementation(libs.bundles.junit.platform)
    implementation(libs.bundles.tomcat)
    implementation(libs.commons.io)

    testCompileOnly project(":opendcs")

    //Includes all jar dependencies from the main OpenDCS build within the opendcs-tests compile
    testCompileOnly fileTree('../../install/build/install/opendcs/dep') {
        include '*.jar'
        builtBy ":install:installDist"
    }

    extra(libs.ch.qos.logback) // need to manually add this to classpath in test tasks

    testImplementation project(":opendcs-schemas")
    testImplementation project(":opendcs-schemas-cwms-oracle")
    testImplementation project(":testing:fixtures")
    testImplementation enforcedPlatform(libs.junit.bom)
    testImplementation(libs.bundles.junit)
    testImplementation(libs.org.mock.server)
    testImplementation(libs.com.bucket4j.bucket4j)
    testRuntimeOnly(libs.bundles.junit.platform)
    
    implementation(project(":opendcs-rest-api"))
    testImplementation(project(":opendcs-rest-api"))
    testImplementation(project(":opendcs-web-ui"))
    testImplementation(libs.bundles.testcontainers)
    testImplementation(libs.bundles.jdbi)
    testImplementation(libs.commons.io)
    testImplementation(libs.apache.derby)
    testImplementation(libs.ch.qos.logback)
    testImplementation(libs.com.faster.xml.jackson.databind)
    testImplementation(libs.webcompere.system.stubs.jupiter)
    testImplementation(libs.rest.assured)
    testImplementation(libs.json.jackson)
    testImplementation(libs.commons.lang)
    testImplementation(libs.mockito.core)
    testImplementation(libs.mockito.junit.jupiter)
    testImplementation(libs.hamcrest)
    testImplementation(libs.nimbus)
    testImplementation(libs.jwt)
    testImplementation(libs.swagger.parser)
    testImplementation(libs.swagger.models)
    testImplementation(libs.selenium)
    testImplementation(libs.web.driver.manager)
    testImplementation(libs.jdbi)
    testRuntimeOnly(libs.byte.buddy)
    runtimeOnly(libs.com.faster.xml.jackson.databind) // for swagger compatibility
    runtimeOnly(libs.com.faster.xml.jackson.annotations) // for swagger compatibility


    modules {
        module("io.swagger.core.v3:swagger-annotations") {
            replacedBy("io.swagger.core.v3:swagger-annotations-jakarta")
        }
        module("io.swagger.core.v3:swagger-models") {
            replacedBy("io.swagger.core.v3:swagger-models-jakarta")
        }
        module("io.swagger.core.v3:swagger-core") {
            replacedBy("io.swagger.core.v3:swagger-core-jakarta")
        }
    }
}

def genConfig = tasks.register('generateConfig', Copy) {
    doFirst {
        project.delete layout.buildDirectory.file("tomcat/")
        project.mkdir layout.buildDirectory.file("tomcat/logs")
    }
    from 'src/test/resources/tomcat'
    into layout.buildDirectory.file("tomcat")
    rename "tomcat-server.xml", "server.xml"
    inputs.dir "src/test/resources"
    outputs.dir layout.buildDirectory.file("tomcat/conf")
}


def testEngines = (project.findProperty("opendcs.test.engine")?: "OpenDCS-XML,OpenDCS-Postgres,OpenDCS-Oracle,CWMS-Oracle").split(",")

test {
    useJUnitPlatform()
    // The below registered test tasks do all the real work.
    exclude '**/*'
}

testEngines.each { engine ->
    def tmpdir = "${project.layout.buildDirectory.get()}/runs/${engine}/tmp"

    def task = tasks.register("test"+engine.replace("-",""), Test) { task ->
        dependsOn ":install:installDist"
        dependsOn ":opendcs:test"
        dependsOn(":opendcs-rest-api:war")
        dependsOn(":opendcs-web-ui:war")
        dependsOn genConfig
        useJUnitPlatform()

        // with gradle 9.2 it seems we need to set these manually
        testClassesDirs = sourceSets.test.output.classesDirs
        classpath = sourceSets.test.runtimeClasspath

        enabled = gradle.startParameter.taskNames.contains(":testing:" + project.name + ":test") ||
                  gradle.startParameter.taskNames.contains(":testing:" + project.name + ":" +task.name)
        doFirst {
            project.mkdir(tmpdir)
        }
        def install = project(":install")
        def stageDir = install.layout.getBuildDirectory().dir("install/${install.distributions.main.distributionBaseName.get()}").get()
        def classPath = (project(':opendcs').jar.outputs.files
                    + project(":opendcs").configurations.runtimeClasspath
                    + configurations.extra).asPath
        inputs.property "opendcs.test.engine", engine

        systemProperties += project.properties.findAll {k, v -> k.startsWith("opendcs")}
        systemProperties += project.properties.findAll {k, v -> k.startsWith("testcontainer")}
        systemProperties += ["opendcs.test.engine": engine]

        jvmArgs += "-Dorg.apache.tomcat.util.digester.PROPERTY_SOURCE=org.apache.tomcat.util.digester.EnvironmentPropertySource"
        jvmArgs += "-Dcatalina.base=${layout.buildDirectory.file("tomcat")}"
        jvmArgs += "-Dopendcs.test.integration.db=${engine}"
        def restWar = project(':opendcs-rest-api').tasks.named('war').get().archiveFile.get().asFile.getAbsolutePath()
        def guiWar = project(':opendcs-web-ui').tasks.named('war').get().archiveFile.get().asFile.getAbsolutePath()
        jvmArgs += "-Dopendcs.restapi.warfile=${restWar}"
        jvmArgs += "-Dopendcs.gui.warfile=${guiWar}"

        jvmArgs += "-Djava.io.tmpdir=${tmpdir}"
        jvmArgs += "-Dbuild.dir=${project.layout.buildDirectory.get()}"
        jvmArgs += "-Dopendcs.test.classpath=${classPath}"
        jvmArgs += "-Dresource.dir=${projectDir}/src/test/resources" // TODO: make clearer
        jvmArgs += "-DDCSTOOL_HOME=${stageDir}"
        jvmArgs += "-Dlogback.configurationFile=${projectDir}/src/test/test-config/logback-test-driver.xml"
        jvmArgs += "-DAPP_NAME=tests"
        jvmArgs += "-DtestLoggerPort="+ project.findProperty("testLoggerPort") ?: "35356"
        jvmArgs += "-Djava.security.manager=allow"

        reports {
            html {}
            junitXml {}
        }
        jacoco {
            excludes += commonJacocoExcludes
        }
    }
    test.dependsOn task

    def jtr = tasks.register("jacocoTestReport"+engine.replace("-",""), JacocoReport) {
        dependsOn task
        //additionalSourceDirs files(project(":opendcs").sourceSets.main.java.srcDirs)
        //additionalClassDirs files(project(":opendcs").sourceSets.main.output)
        sourceSets project(":opendcs").sourceSets.main
        executionData = files("${buildDir}/jacoco/test"+engine.replace("-","")+".exec") // Use the .exec file from the custom test task
        reports {
            html {
                outputLocation = jacoco.reportsDirectory.dir("jacoco-${engine}-html")
            }
            xml {
                outputLocation = jacoco.reportsDirectory.file("jacoco-${engine}.xml")
            }

        }



        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: commonJacocoExcludes)
        }))

    }

    task.configure {
        finalizedBy jtr
    }
}


tasks.register('runApi', JavaExec) {
    dependsOn build
    def restWar = project(':opendcs-rest-api').tasks.named('war').get().archiveFile.get().asFile.getAbsolutePath()
    def guiWar = project(':opendcs-web-ui').tasks.named('war').get().archiveFile.get().asFile.getAbsolutePath()
    inputs.files(restWar,guiWar)
    group "application"
    dependsOn(':opendcs-rest-api:war', ':opendcs-web-ui:war')
    dependsOn generateConfig
    dependsOn ":install:installDist"
    classpath += sourceSets.test.runtimeClasspath
    classpath += configurations.testRuntimeClasspath

    mainClass = "org.opendcs.fixtures.TomcatServer"
    systemProperties += project.properties.findAll { k, v -> k.toLowerCase().startsWith("testcontainer") }
    systemProperties += project.properties.findAll { k, v -> k.toLowerCase().startsWith("opendcs") }
    systemProperties.put("org.apache.tomcat.util.digester.PROPERTY_SOURCE", "org.apache.tomcat.util.digester.EnvironmentPropertySource")
    systemProperties.put("java.util.logging.config.file", "build/tomcat/conf/logging.properties")
    systemProperties.put("DCSTOOL_USERDIR", "build/resources/test/tomcat/conf")

    def port = project.findProperty("opendcs.tomcat.port") ?: "7000"
    def databaseType = project.findProperty("opendcs.test.integration.db") ?: "OpenDCS-Postgres"
    def tomcatDir = layout.buildDirectory.file("tomcat").get()
    args tomcatDir, port, restWar, guiWar, databaseType
    def install = project(":install")
    def odcsInstallDir = install.layout
                                .getBuildDirectory()
                                .dir("install/${install.distributions.main.distributionBaseName.get()}")
                                .get()
    systemProperties.put("DCSTOOL_HOME", odcsInstallDir.toString())
}

tasks.register('owaspZap', JavaExec) {
    dependsOn build
    def restWar = project(':opendcs-rest-api').tasks.named('war').get().archiveFile.get().asFile.getAbsolutePath()
    def guiWar = project(':opendcs-web-ui').tasks.named('war').get().archiveFile.get().asFile.getAbsolutePath()
    group "application"
    dependsOn(':opendcs-rest-api:war', ':opendcs-web-ui:war')
    dependsOn generateConfig
    dependsOn ":install:installDist"

    mainClass = "org.opendcs.odcsapi.owasp.OwaspZap"
    classpath += sourceSets.test.runtimeClasspath
    classpath += configurations.testRuntimeClasspath
    systemProperties += project.properties.findAll { k, v -> k.startsWith("testcontainer") }
    systemProperties += project.properties.findAll { k, v -> k.startsWith("opendcs") }
    systemProperties.put("org.apache.tomcat.util.digester.PROPERTY_SOURCE", "org.apache.tomcat.util.digester.EnvironmentPropertySource")
    systemProperties.put("DCSTOOL_USERDIR", "build/resources/test/owasp")
    systemProperties.put("java.util.logging.config.file", "build/tomcat/conf/logging.properties")

    def port = project.findProperty("opendcs.tomcat.port") ?: "8910"
    def databaseType = project.findProperty("opendcs.test.integration.db") ?: "OpenDCS-Postgres"
    def tomcatDir = layout.buildDirectory.file("tomcat").get()
    args tomcatDir, port, restWar, guiWar, databaseType
    def install = project(":install")
    def odcsInstallDir = install.layout
                                .getBuildDirectory()
                                .dir("install/${install.distributions.main.distributionBaseName.get()}")
                                .get()
    systemProperties.put("DCSTOOL_HOME", odcsInstallDir.toString())
}
