plugins {
    id 'opendcs.java-conventions'
    id 'opendcs.deps-conventions'
    id 'opendcs.publishing-conventions'
}

dependencies {
    api(project(":opendcs"))
    api(enforcedPlatform(libs.junit.bom))
    api(libs.bundles.junit)
    api(libs.commons.io)
    api(libs.apache.derby)
    api(libs.webcompere.system.stubs.jupiter)
    api(libs.bundles.testcontainers)
    api(libs.hec.testcontainers.cwms)
    api(libs.bundles.jdbi)

    testImplementation enforcedPlatform(libs.junit.bom)
    testImplementation(libs.bundles.junit)
    testImplementation(libs.bundles.junit.platform)

    implementation(libs.hec.monolith) {
        exclude group: "codebase"
        exclude group: "org.jdom"
        exclude group: "org.bouncycastle"
        exclude group: "org.eclipse.jetty.aggregate"
        exclude group: "javax.servlet"
        exclude group: "mil.army.usace.hec.swingx"
        exclude group: "com.fifesoft"
        exclude group: "org.python"
        exclude group: "com.vividsolutions"
        exclude group: "mil.army.usace.hec", module: "context-specific-help"
        exclude group: "mil.army.usace.hec", module: "hec-geojson"
        exclude group: "javax.mail"
        exclude group: "org.apache.poi"
        exclude group: "net.java.dev.jna"
        exclude group: "org.apache-commons", module: "common-vfs2"
        exclude group: "org.jfree"
        exclude group: "com.mrsid"
        exclude group: "javax.help"
        exclude group: "org.jooq.pro"
    }
}

configurations.all {
    exclude group: 'org.eclipse.jetty', module: 'jetty-jsp'
    exclude group: "com.oracle.database.jdbc", module: "jmscommon"
    exclude group: "org.jooq.pro", module: "jooq-meta"
    exclude group: "org.jooq.pro", module: "jooq-codegen"
    exclude group: "com.mrsid"
    exclude group: "javax.help"
    exclude group: "javax.media"
    exclude group: "com.sun.media"
    exclude group: "org.eclipse.jetty"
    exclude group: "javax.xml.bind", module: "jaxb-api"
    exclude group: "javax.transaction", module: "jta"
    exclude group: "javax.xml.stream", module: "stax-api"
}

// Force archive names to match publishing name.
// Otherwise maven central validation fails.
base {
    archivesName = "opendcs-integrationtesting-fixtures"
}

test
{
    jvmArgs += "-Dopendcs.test.engine=OpenDCS-Enabled"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'opendcs-integrationtesting-fixtures'
            from components.java

            pom {
                name = 'OpenDCS Test Fixtures'
                description = 'Database Framework, assertions, and other utilities required to implement integration tests against OpenDCS implementations.'
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    // The value 'enforced-platform' is provided in the validation
    // error message you got
    suppressedValidationErrors.add('enforced-platform')
}
