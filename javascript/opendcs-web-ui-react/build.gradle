plugins {
    id 'base'
    id 'opendcs.deps-conventions'
    id 'opendcs.sonar-conventions'
    id 'opendcs.version-conventions'
    id 'com.github.node-gradle.node' version '7.1.0'
}

configurations {
    api
}

dependencies {
    api(project(path: ":api-client-typescript", configuration: "lib"))
}

//npmSetup.dependsOn configurations.api

def buildUi = tasks.register("buildUi", NpxTask) {
    dependsOn npmInstall
    dependsOn configurations.api
    command = "npm"
    args = ['run', 'build']
    environment = [
        "IN_GRADLE": "1"
    ]
    outputs.dir(file("dist"))
}

def testUi = tasks.register("testStorybook", NpxTask) {
    group "Verification"
    dependsOn buildUi
    dependsOn configurations.api
    command = "npm"
    environment = [
        "IN_GRADLE": "1"
    ]
    args = ['run', 'test-storybook', '--', '--coverage', '--reporter=junit', '--outputFile', 'build/test-results/test-storybook.xml']
}

tasks.register("test") {
    group "Verification"
    dependsOn testUi
    dependsOn configurations.api
}

node {
    // Version of Node.js to install. Required.
    version = '24.12.0'
    
   
    // Whether to download Node.js if it is not already installed. Defaults to true.
    download = true
    
    // The working directory for Node.js. Defaults to the project directory.
    
    // Configure a proxy for the download. Optional.
    // distBaseUrl = 'https://custom.node.dist.mirror'
}

build.dependsOn buildUi

def runUiDev = tasks.register("runUiDev", NpxTask) {
    group 'application'
    description 'Runs npm run dev to begin UI development. Assumes an OpenDCS Api instances in available on localhost:7000/odcsapi'
    dependsOn npmInstall
    dependsOn configurations.api  
    command = "npm"
    args = ['run', 'dev']
    environment = [
        "IN_GRADLE": "1"
    ]
}
