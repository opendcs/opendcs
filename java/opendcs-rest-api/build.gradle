/*
 *  Copyright 2025 OpenDCS Consortium and its Contributors
 *
 *  Licensed under the Apache License, Version 2.0 (the "License")
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

plugins {
    id "opendcs-rest-api.java-conventions"
    id "opendcs-rest-api.deps-conventions"
    id "opendcs-rest-api.publishing-conventions"
    id "war"
    id "io.swagger.core.v3.swagger-gradle-plugin" version "2.2.41"
}

configurations {
    webjars
    api_spec {
        canBeResolved = false // Consumers will resolve, not this config
        canBeConsumed = true // This config can be consumed by others
    }
}

dependencies {
    compileOnly(libs.auto.service)
    annotationProcessor(libs.auto.service)

    implementation(libs.opendcs) {
        exclude group: "javax.media", module: "jai_core"
        exclude group: "javax.media", module: "jai_codec"
        exclude group: "org.jooq.pro", module: "jooq-meta"
        exclude group: "org.jooq.pro", module: "jooq-codegen"
        exclude group: "com.oracle.database.jdbc", module: "jmscommon"
        exclude group: "com.oracle.database.jdbc", module: "aqapi"
        exclude group: "com.oracle.database.jdbc", module: "xmlparserv2"
        exclude group: "com.oracle.database.jdbc", module: "xdb"
        exclude group: "com.sun.media", module: "jai_imageio"
        exclude group: "com.mrsid", module: "mrsidreader"
        exclude group: "org.eclipse.jetty", module: "jetty-annotations"
    }
    implementation(libs.opendcs.api)
    implementation(libs.jdbi)
    swaggerDeps(libs.servlet.api)
    swaggerDeps(libs.rs.api)
    swaggerDeps(libs.swagger.jaxrs2)
    compileOnly(libs.servlet.api)
    compileOnly(libs.rs.api)
    implementation(libs.json.jackson)
    implementation(libs.jersey.container.servlet)
    implementation(libs.nimbus)
    implementation(libs.swagger.jaxrs2)
    implementation(libs.jwt)
    implementation(libs.jersey.hk2)
    implementation(libs.slf4j.api)
    implementation(libs.jackson)
    implementation(libs.jakarta.xml.bind.api)
    runtimeOnly(libs.slf4j.jdk14)
    runtimeOnly(libs.org.glassfish.jaxb.runtime)
    runtimeOnly(libs.com.faster.xml.jackson.annotations) // updated for swagger compatibility

    webjars(libs.swagger.ui)

    testImplementation enforcedPlatform(libs.junit.bom)
    testImplementation(libs.bundles.junit)
    testImplementation(libs.bundles.junit.platform)
    testImplementation(libs.jersey.test)
    testImplementation(libs.mockito.core)
    testImplementation(libs.mockito.junit.jupiter)
}

tasks.register('extractWebJars', Copy) {
    from zipTree(configurations.webjars.singleFile)

    into file("${layout.buildDirectory.get().asFile}/generated/webapp/swaggerui")
    includeEmptyDirs = false
    eachFile {
        path -= ~/^.+?\/.+?\/.+?\/.+?\/.+?\//
    }
    exclude "**/swagger-initializer.js"
    includeEmptyDirs = false
}

compileTestJava {
    dependsOn extractWebJars
}

javadoc {
    dependsOn extractWebJars
}

jar {
    dependsOn extractWebJars
}

war {
    dependsOn extractWebJars
    from "${layout.buildDirectory.get().asFile}/generated/webapp"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = war.archiveBaseName.get()
            from components.java
        }
    }
}

sonarqube {
    properties {
        property 'sonar.coverage.jacoco.xmlReportPaths', "${layout.buildDirectory.get().asFile.toString()}/reports/jacoco/test/jacocoTestReport.xml," +
                "../opendcs-integration-test/build/reports/jacoco/integrationTestReportCWMS/integrationTestReportCWMS.xml," +
                "../opendcs-integration-test/build/reports/jacoco/integrationTestReportOpenTSDB/integrationTestReportOpenTSDB.xml"
    }
}


// task to generate OpenAPI JSON file
resolve {
    classpath = sourceSets.main.runtimeClasspath
    outputFileName = 'opendcs-openapi'
    outputFormat = providers.gradleProperty('outputFormat').getOrElse('JSON')
    prettyPrint = 'TRUE'
    resourcePackages = ['org.opendcs.odcsapi.res', 'org.opendcs.odcsapi.beans', 'org.opendcs.odcsapi.sec']
    outputDir = file('build/swagger/')
}

def genSpec = tasks.register('generateOpenAPI') {
    group = "documentation"
    description = "Generates the Swagger OpenAPI JSON file without running the application"
    dependsOn('resolve')
    outputs.files(
        resolve.outputDir
               .file(resolve.outputFileName.get() +
                     "." +
                     resolve.outputFormat.get().toString().toLowerCase()))
}

artifacts {
    add("api_spec", genSpec)
}
