plugins {
    id 'base'
}
ext {
    osWin = rootProject.ext.osWin
    pythonExe = osWin ? "python.exe" : "python3"
    docsVenv = "$buildDir/sphinx"
    docsDir = projectDir
    venvPath = (osWin ? "${docsVenv}/Scripts" : "${docsVenv}/bin")
    noDocs = project.findProperty("no.docs") ?: false
    havePython = project.objects.property(Boolean)
}

import javax.inject.Inject

abstract class CheckPython extends DefaultTask {
    private ExecOperations execOperations

    @Inject
    CheckPython(ExecOperations execOperations) {
        this.execOperations = execOperations
    }

    @TaskAction
    void doAction() {
        def result = execOperations.exec {
            ignoreExitValue = true
            if (project.ext.osWin) {
                executable "cmd"
                args += "/c"
                args += project.ext.pythonExe
            } else {
                executable project.ext.pythonExe
            }
            args += "-c"
            args += "import sys; print('checking for python'); sys.exit(0)"
        }

        project.ext.havePython.set(result.getExitValue() == 0)
    }
}

configurations {
    docs {
        canBeConsumed = true
        canBeResolved = false
    }
}

tasks.register("checkPython", CheckPython) {
    enabled = !noDocs
}

task prepVenv(type: Exec) {
    enabled = !noDocs
    executable pythonExe
    workingDir projectDir
    args += "-m"
    args += "venv"
    args += docsVenv
    outputs.dir(docsVenv)
           .withPropertyName('outputDir')
}

task installReqs(type: Exec) {
    dependsOn prepVenv
    dependsOn checkPython
    enabled = !noDocs
    def theArgs = osWin ? ["/c","pip"]
                        : []
    def cmd = osWin ? "cmd" : venvPath+"/pip"
    executable cmd
    workingDir projectDir
    args += theArgs
    args += "install"
    args += "-r"
    args += "${docsDir}/requirements.txt"
    environment "PATH": "${venvPath}${File.pathSeparator}${System.getenv('PATH')}"
    environment "VENV_PATH", venvPath
}


tasks.addRule("Pattern: buildDocs<ID>") { String taskName ->
    def builder = taskName.toLowerCase().replace("builddocs","")
    if (taskName.startsWith("buildDocs")) {

        def theArgs = osWin ? ["/c","make.bat",builder]
                         : [builder]
        def cmd = osWin ? "cmd" : "make"
        task(taskName, type: Exec) {
            dependsOn installReqs
            dependsOn checkPython
            onlyIf { !noDocs && havePython.getOrElse(false) }
            executable cmd
            workingDir projectDir
            args = theArgs
            environment "PATH": "${venvPath}${File.pathSeparator}${System.getenv('PATH')}"

            inputs.files(fileTree('source'))
                .withPropertyName('sourceFiles')
                .withPathSensitivity(PathSensitivity.RELATIVE)
            outputs.dir("${buildDir}/${builder}")
                    .withPropertyName('outputDir')
        }
    }
}

task buildDocs {
    onlyIf { !noDocs }
    dependsOn 'buildDocsHtml'
}

build.dependsOn buildDocs

artifacts {
    docs(file("${buildDir}/html")) {
        builtBy(buildDocs)
    }
}